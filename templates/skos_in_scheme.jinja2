PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX {{ prefix }}: <{{ uri }}>
PREFIX {{ prefix }}-sh: <{{ uri }}shapes/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sh: <http://www.w3.org/ns/shacl#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

construct {
    {{ prefix }}-sh:{{ class }}Shape sh:property ?shapeUri .
    ?shapeUri a sh:PropertyShape ;
    		rdfs:comment ?comment ;
    		sh:path ?property ;
    		sh:class skos:Concept ;
    		sh:property ?inSchemeShapeUri ;
            sh:severity sh:violation;
    		sh:message ?message.
    ?inSchemeShapeUri a sh:PropertyShape;
    		sh:path skos:inScheme;
    		sh:hasValue ?scheme;
			sh:severity sh:violation;
    		sh:message ?schemeMessage.
}
#select * 
where {
    ?property a owl:ObjectProperty ;
        rdfs:range skos:Concept;
    	{{ annotation_property }} ?scheme.
    {?property rdfs:domain {{ prefix }}:{{ class }}.}
    UNION
    {?property rdfs:domain [owl:unionOf (?other1 {{ prefix }}:{{ class }})].}
    UNION
    {?property rdfs:domain [owl:unionOf ({{ prefix }}:{{ class }} ?other2)].}
    UNION
    {?property rdfs:domain [owl:unionOf ({{ prefix }}:{{ class }} ?other3 ?another1)].}
    UNION
    {?property rdfs:domain [owl:unionOf (?other4 {{ prefix }}:{{ class }} ?another2 )].}
    UNION
    {?property rdfs:domain [owl:unionOf (?other5 ?another3 {{ prefix }}:{{ class }})].}    
    OPTIONAL {?property rdfs:comment ?comment.}
    BIND( URI(CONCAT("{{ uri }}shapes/",
                UCASE(SUBSTR(REPLACE(STR(?property), ".*(\\/|#){1}(\\w*)","$2"),1,1)),
                SUBSTR(REPLACE(STR(?property), ".*(\\/|#){1}(\\w*)","$2"),2),
    			"SKOS"
            		)
        	  ) AS ?shapeUri)
    BIND( URI(CONCAT(STR(?shapeUri),"inScheme")) AS ?inSchemeShapeUri)    
    BIND( STRLANG(CONCAT(
            REPLACE(STR(?property), ".*(\\/|#){1}(\\w*)","$2"),
            ": The instance of {{ class }} has a ",
            REPLACE(STR(?property), ".*(\\/|#){1}(\\w*)","$2"),
            " value that is not a SKOS concept."),"en"
        		) AS ?message)
	BIND( STRLANG(CONCAT(
            REPLACE(STR(?property), ".*(\\/|#){1}(\\w*)","$2"),
            ": The instance of {{ class }} has a ",
            REPLACE(STR(?property), ".*(\\/|#){1}(\\w*)","$2"),
            " value that is not one of the predefined values and cannot be converted into a SKOS concept on this list: ",
            STR(?scheme),
			"."),"en"
        		) AS ?schemeMessage)
    FILTER NOT EXISTS {?property owl:deprecated "true"^^xsd:boolean}
  }
                